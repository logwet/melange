plugins {
    id 'application'
    id "io.freefair.lombok" version "6.3.0"
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id 'edu.sc.seis.launch4j' version '2.5.1'
}

group = project.maven_group
archivesBaseName = project.archives_base_name
version = project.base_version + project.version_suffix

configurations {
    antTask
}

repositories {
    mavenCentral()
    maven {
        url "https://www.jetbrains.com/intellij-repository/releases"
    }
    maven {
        url "https://cache-redirector.jetbrains.com/intellij-dependencies"
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")

    implementation("com.vdurmont:semver4j:3.1.0")

    implementation("ch.qos.logback:logback-classic:1.2.10")

    implementation("org.apache.commons:commons-lang3:3.12.0")

    implementation("com.google.guava:guava:31.0.1-jre")

    //noinspection GradlePackageUpdate
    implementation("com.github.ben-manes.caffeine:caffeine:2.9.3")

    implementation("net.harawata:appdirs:1.2.1")

    implementation("com.fasterxml.jackson.core:jackson-databind:2.13.1")

    implementation('com.aparapi:aparapi:3.0.0')

    implementation("com.github.wendykierp:JTransforms:3.1")

    implementation("com.mojang:brigadier:1.0.18")

    implementation("com.github.twitch4j:twitch4j:1.7.0")

//    implementation("com.github.hube12:SEED:07d6f2502c0265703fe3a44a91af09141346f249")

    implementation("com.formdev:flatlaf:2.0-rc1")

    compileOnly("com.jetbrains.intellij.java:java-gui-forms-rt:212.5712.34")
    antTask("com.jetbrains.intellij.java:java-compiler-ant-tasks:212.5712.34")
}

application {
    mainClass = "me.logwet.melange.Main"
}

processResources {
    filesMatching("melange/metadata.json") {
        expand project.properties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    def targetVersion = 8

    it.sourceCompatibility = JavaVersion.toVersion(targetVersion)
    it.targetCompatibility = JavaVersion.toVersion(targetVersion)

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

task compileJava(type: JavaCompile, overwrite: true, dependsOn: configurations.implementation.getTaskDependencyFromProjectDependency(true, 'jar')) {
    doLast {
        project.sourceSets.main.output.classesDirs.each { project.mkdir(it) }
        ant.taskdef name: 'javac2', classname: 'com.intellij.ant.Javac2', classpath: configurations.antTask.asPath
        ant.javac2 srcdir: project.sourceSets.main.java.srcDirs.join(':'),
                classpath: project.sourceSets.main.compileClasspath.asPath,
                destdir: project.sourceSets.main.output.classesDirs[0],
                source: sourceCompatibility,
                target: targetCompatibility,
                includeAntRuntime: false
    }
}

shadowJar {
    archiveClassifier.set("")
}

jar {
    from("LICENSE") {}
    from("README.md") {}

    archiveClassifier.set("thin")

    //noinspection GroovyAssignabilityCheck
    manifest {
        attributes(
                "Main-Class": "me.logwet.melange.Main",
                "Implementation-Version": project.base_version,
                "SplashScreen-Image": "melange/splashscreen.jpg"
        )
    }
}

launch4j {
    outputDir = "libs"
    outfile = "melange-${project.version}.exe"
    mainClassName = "me.logwet.melange.Main"
    icon = "${projectDir}/assets/icon.ico"
    splashFileName = "${projectDir}/assets/splashscreen.bmp"
    version = project.base_version
    textVersion = project.version
    companyName = "logwet"
    copyright = "Copyright (C) 2022 logwet"
    copyConfigurable = []
    jarTask = project.tasks.shadowJar
    jreMinVersion = "1.8.0"
    maxHeapSize = 50
}

build {
    it.dependsOn createExe
}

test {
    useJUnitPlatform()
}
